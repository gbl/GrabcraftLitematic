plugins {
	id 'fabric-loom' version '0.4-SNAPSHOT'
	id "com.modrinth.minotaur" version "1.1.0"
}

repositories {
	maven {
		url = "https://maven.fabricmc.net"
	}
	maven {
		url = "https://minecraft.guntram.de/maven/"
	}
	maven {
        name = 'CottonMC'
        url = 'http://server.bbkr.space:8081/artifactory/libs-release'
	}    
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext.Versions = new Properties()
Versions.load(file("Versionfiles/mcversion-1.16.4.properties").newReader())

archivesBaseName = "grabcraft-litematic"
ext.projectVersion = "0.4"

version = "${Versions['minecraft_version']}-fabric${Versions['fabric_versiononly']}-${project.projectVersion}"

minecraft {
    refmapName = "Grabcraft-Litematic-refmap.json";
}

processResources {
	inputs.property "version", project.version

        from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

dependencies {
    minecraft  "com.mojang:minecraft:${Versions['minecraft_version']}"
    mappings   "net.fabricmc:yarn:${Versions['yarn_mappings']}:v2"
    modImplementation "net.fabricmc:fabric-loader:${Versions['loader_version']}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${Versions['fabric_version']}"
    compile "com.oneandone:composite-jks:1.0"
    include "com.oneandone:composite-jks:1.0"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

import com.modrinth.minotaur.TaskModrinthUpload

task publishModrinth (type: TaskModrinthUpload){

    token = System.getenv("MODRINTH_TOKEN")
    projectId = 'CpRqX7TP'
    versionNumber = project.version
    versionName = project.archivesBaseName
    releaseType = 'release'
    uploadFile = remapJar // This is the java jar task
    addGameVersion("${Versions['minecraft_version']}")
    addLoader('fabric')
}

